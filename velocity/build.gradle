buildscript {
    dependencies {
        classpath "info.u-team.curse_gradle_uploader:curse_gradle_uploader:${curse_gradle_uploader_version}"
    }
}

plugins {
    id 'java'
    id 'com.modrinth.minotaur' version "${minotaur_version}"
    id 'io.papermc.hangar-publish-plugin' version "${hangar_publish_version}"
}

apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'info.u_team.curse_gradle_uploader'

apply from: "https://raw.githubusercontent.com/henkelmax/mod-gradle-scripts/${mod_gradle_script_version}/mod.gradle"

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
}

dependencies {
    compileOnly "com.velocitypowered:velocity-api:${velocity_version}"
    annotationProcessor "com.velocitypowered:velocity-api:${velocity_version}"
    compileOnly("com.electronwill.night-config:toml:${night_config_version}")
}

sourceSets {
    template {
        java {
            srcDir 'src/template/java'
        }
    }
    main {
        compileClasspath += sourceSets.template.output
        runtimeClasspath += sourceSets.template.output
    }
}

tasks.register('generateJava', Copy) {
    from 'src/template/java'
    into "${layout.buildDirectory.asFile.get()}/generated/java"
    expand 'mod_version': mod_version
}

sourceSets.main.java {
    srcDir "${layout.buildDirectory.asFile.get()}/generated/java"
}

compileJava.dependsOn generateJava
build.dependsOn shadowJar
